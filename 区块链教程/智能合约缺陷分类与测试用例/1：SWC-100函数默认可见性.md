# 1：SWC-100函数默认可见性

## 缺陷分类

ID：SWC-100

缺陷类型：Function Default Visibility（函数默认可见性）

危害程度：高

## 漏洞描述

默认情况下，合约中没有指定函数可见性类型的函数是公共的（public）。如果开发人员忘记设置可见性，而恶意用户能够进行未经授权或非预期的状态更改，那么这可能会导致漏洞。

## 合约例子

**文件名：visibility_not_set.sol**

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.4.24;

// 函数默认可见性缺陷
contract HashForEther {
    // 判断赢家
    function withdrawWinnings() {
        // 如果地址最后4个字节为零则赢
        require(uint32(msg.sender) == 0);
        sendWinnings();
    }

    // 发送以太币
    function sendWinnings() {
        msg.sender.transfer(this.balance);
    }
}
```

这个例子中，`sendWinnings` 函数没有设置可见性类型，默认为 `public`，其它外部合约均可以不经过授权就能访问这个函数提取合约中的剩余以太币，这就是导致漏洞的原因。

## 修复建议

函数可以指定为外部的（external）、公共的（public）、内部（internal）的或私有的（private）。建议您慎重决定哪种可见性类型适合函数，这可以极大地减少合约系统的攻击面。

**文件名：visibility_not_set_fixed.sol**

```
// SPDX-License-Identifier: MIT
pragma solidity ^0.4.24;

// 函数默认可见性缺陷
contract HashForEther {
    // 判断赢家
    function withdrawWinnings() public {
        // 如果地址最后4个字节为零则赢
        require(uint32(msg.sender) == 0);
        sendWinnings();
    }

    // 发送以太币
    function sendWinnings() internal {
        msg.sender.transfer(address(this).balance);
    }
}
```

修复后的合约，`sendWinnings` 函数的可见性类型被设置为 `internal`，这意味着这个函数是内部的，只允许合约的派生合约才能访问这个函数，从而可以避免外部合约的非法访问而导致漏洞。

**注：另外，我们建议使用最新的编译器版本，因为最新的编译器需要严格设置函数的可见性类型才能通过编译，否则编译时会报错，这样就能避免此类缺陷的发生。**

